/*
 * Copyright (c) 2020, Live Media Pty. Ltd. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License
 * is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express
 * or implied. See the License for the specific language governing permissions and limitations under
 * the License.
 */
 
buildscript {
    repositories {
       mavenCentral()
    }
}


plugins {
    id 'java'
    id 'eclipse'
    id 'jacoco'
}


sourceCompatibility = 11
targetCompatibility = 11

repositories {
   mavenCentral()
}

dependencies {

   implementation project(":litterat-bind")
   implementation project(":litterat-model")
   implementation project(":litterat-xpl")
   
   // test data
   implementation project(":litterat-bind-test")

   // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
   implementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.1'
	
   // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
   implementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.1'
	
}

java {
   modularity.inferModulePath = true
}

test {
	useJUnitPlatform()
	
	afterSuite { desc, result ->
    if (!desc.parent)
        println("${result.resultType} " +
            "(${result.testCount} tests, " +
            "${result.successfulTestCount} successes, " +
            "${result.failedTestCount} failures, " +
            "${result.skippedTestCount} skipped)")
    }
}

jacocoTestReport {
	sourceSets project(':litterat-model').sourceSets.main
    sourceSets project(':litterat-xpl').sourceSets.main
    
    
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'io/litterat/test/**')
        }))
    }
}

sourceSets {
    test {
        java.srcDir 'src/main/java'
    }
}

eclipse {
    classpath {
        // remove the source set from the Eclipse classpath
        sourceSets -= [sourceSets.test]
    }
}

